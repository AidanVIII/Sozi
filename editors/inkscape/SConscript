
import os

env = Environment(tools = ["default", "gettext"])

#
# Lists of source files
#

inx_files = Glob("*.inx")
py_files = Glob("*.py")
py_module_files = Glob("sozi/*.py")
glade_files = Glob("sozi/*.glade")
po_files = Glob("sozi/lang/*.po")
icon_files = Glob("sozi/*.png")

Import("player_files")

#
# Set target directory for installation
#

inkscape_extensions_dir = os.environ["HOME"] + "/.config/inkscape/extensions"
inkscape_extensions_sozi_dir = inkscape_extensions_dir + "/sozi"

Alias("install-editor-inkscape", inkscape_extensions_dir)

Install(inkscape_extensions_dir, inx_files + py_files)
Install(inkscape_extensions_sozi_dir, py_module_files + glade_files + icon_files + player_files)

#
# Insert Sozi version in sozi/version.py
#

Import("sozi_version")
py_version_file = Command("sozi/version.py", "sozi/version.py.tpl", "sed s/@SOZI_VERSION@/{0}/ $SOURCE > $TARGET".format(sozi_version))
Install(inkscape_extensions_sozi_dir, py_version_file)

#
# Compile translation files
#

env.POTUpdate("sozi", py_files + py_module_files + glade_files)

mo_files = env.MOFiles(po_files)

for mo_file in mo_files:
    lang = os.path.splitext(mo_file.name)[0]
    InstallAs("{0}/lang/{1}/LC_MESSAGES/sozi.mo".format(inkscape_extensions_sozi_dir, lang), mo_file)

#
# Install license files
#

Import("license_files")
Install(inkscape_extensions_sozi_dir, license_files)

