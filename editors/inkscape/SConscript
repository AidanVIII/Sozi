
import os

env = Environment(tools = ["default", "gettext", "zip_relative", "textfile"])

Import("prefix user_prefix common_install")

#
# Lists of source files
#

inx_files = Glob("*.inx")
py_files = Glob("*.py")
py_module_files = Glob("sozi/*.py")
glade_files = Glob("sozi/*.glade")
po_files = Glob("sozi/lang/*.po")
icon_files = Glob("sozi/*.png")

Import("player_files license_files")


#
# Insert Sozi version in sozi/version.py
#

Import("sozi_version")
py_version_file = env.Substfile("sozi/version.py.in", SUBST_DICT = {"@SOZI_VERSION@" : sozi_version})


#
# Compile translation files
#

env.POTUpdate("sozi", py_files + py_module_files + glade_files)

mo_files = env.MOFiles(po_files)


#
# Set target directories for installation and release bundle
#

def install_to(dir):
    sozi_dir = dir + "/sozi"

    Install(dir, inx_files + py_files)
    Install(sozi_dir, py_module_files + glade_files + icon_files + player_files + license_files)
    Install(sozi_dir, py_version_file)

    for mo_file in mo_files:
        lang = os.path.splitext(mo_file.name)[0]
        InstallAs("{0}/lang/{1}/LC_MESSAGES/sozi.mo".format(sozi_dir, lang), mo_file)

if prefix == user_prefix:
    inkscape_extensions_dir = os.environ["HOME"] + "/.config/inkscape/extensions"
else:
    inkscape_extensions_dir = prefix + "/share/inkscape/extensions"

Alias("install-editor-inkscape", [inkscape_extensions_dir, common_install])
install_to(inkscape_extensions_dir)

release_dir = "sozi-release-" + sozi_version;
install_to(release_dir)
env.ZipRelative(release_dir + ".zip", release_dir)

